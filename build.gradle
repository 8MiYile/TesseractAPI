plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
    id 'java'
}

evaluationDependsOnChildren()

group = 'com.github.gregtech-intergalactical' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'tesseract'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

configurations {
    embed
}

shadowJar {
    configurations = [project.configurations.embed]
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'it.unimi.dsi:fastutil:8.4.1'

    // Test dependencies:
    testImplementation 'junit:junit:4.11'

    // Embedded dependencies:
    // Those will be included in the fat jar.
    embed project(path: ':forge', configuration: 'archives')
}

// Maven publishing:
// (includes universal and common)
publishing {
    publications {
        universal(MavenPublication) {
            artifact shadowJar
        }
        base(MavenPublication) {
            artifact jar
        }
    }

    repositories {
        maven {
            url = 'https://files.axelandre42.ovh/maven/'
            authentication {
                basic(BasicAuthentication)
            }

            if (System.env.MAVEN_USERNAME && System.env.MAVEN_PASSWORD) {
                credentials {
                    username = System.env.USERNAME
                    password = System.env.PASSWORD
                }
            }
        }
    }
}
